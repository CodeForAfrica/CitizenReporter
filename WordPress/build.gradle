buildscript {
  System.properties['com.android.build.gradle.overrideVersionCheck'] = 'true'
  repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
    maven {
      url "https://jitpack.io"
    }
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:2.2.1'
    classpath 'com.google.gms:google-services:3.0.0'
    classpath 'com.github.nrudenko:gradle-android-cq-plugin:0.1+'
    classpath 'io.fabric.tools:gradle:1.+'
  }
}

repositories {
  mavenCentral()
  maven { url 'http://wordpress-mobile.github.io/WordPress-Android' }
  maven { url 'https://maven.fabric.io/public' }
  jcenter()
  maven { url "https://jitpack.io" }
}

apply plugin: 'com.android.application'
apply plugin: 'android-cq'
apply plugin: 'io.fabric'

android {
  useLibrary 'org.apache.http.legacy'
  packagingOptions {
    exclude "META-INF/LICENSE.txt"
    exclude "META-INF/NOTICE.txt"
  }

  compileSdkVersion 24
  buildToolsVersion "24.0.1"

  defaultConfig {
    applicationId "org.codeforafrica.citizenreporter.starreports"
    versionName "4.2.6"
    versionCode 200
    minSdkVersion 16
    targetSdkVersion 23
    multiDexEnabled = true

    buildConfigField "String", "APP_PN_KEY", "\"org.wordpress.android.playstore\""
  }

  productFlavors {
    vanilla {}
    zbetagroup {
      buildConfigField "String", "APP_PN_KEY", "\"org.wordpress.android.beta.build\""
      applicationId "org.wordpress.android.beta"
    }
    eNCA {
      minSdkVersion 16
      applicationId 'org.codeforafrica.citizenreporter.eNCA'
      targetSdkVersion 23
      versionName '1.0.7'
      versionCode 200
    }
    starreports {
      minSdkVersion 16
      applicationId 'org.codeforafrica.citizenreporter.starreports'
      targetSdkVersion 23
      versionName '1.0.9'
      versionCode 202
    }
  }

  buildTypes {
    release {
      // Proguard is only used to fix an issue with some Samsung device
      // https://github.com/wordpress-mobile/WordPress-Android/issues/2151
      //minifyEnabled true
      //shrinkResources true
      //proguardFile 'proguard.cfg'
    }

    debug {
      buildConfigField "String", "APP_PN_KEY", "\"org.wordpress.android.debug.build\""
      ext.enableCrashlytics = false
    }
  }
  dexOptions {
    jumboMode true
    incremental false
    javaMaxHeapSize "2048M"
    preDexLibraries = false
  }
}

dependencies {
  compile fileTree(include: '*.jar', dir: 'libs')
  compile 'com.crashlytics.sdk.android:crashlytics:2.6.5'
  // Provided by maven central
  compile('org.wordpress:mediapicker:1.2.3') {
    exclude group: 'com.android.support'
  }
  compile 'com.github.markushi:circlebutton:1.1'
  compile 'com.google.code.gson:gson:2.2.2'
  compile 'org.ccil.cowan.tagsoup:tagsoup:1.2.1'
  compile 'com.android.support:support-v13:24.2.1'
  compile 'com.android.support:appcompat-v7:24.0.+'
  compile 'com.android.support:cardview-v7:24.2.1'
  compile 'com.android.support:recyclerview-v7:24.2.1'
  compile 'com.github.chrisbanes.photoview:library:1.2.3'
  compile 'com.helpshift:android-aar:3.8.0'
  compile 'commons-lang:commons-lang:2.6'
  compile 'com.cocosw:undobar:1.6@aar'
  compile 'de.greenrobot:eventbus:2.4.0'
  compile('com.getbase:floatingactionbutton:1.9.0') {
    exclude module: 'support-annotations'
  }
  compile 'com.automattic:rest:1.0.1'
  compile 'org.wordpress:graphview:3.3.0'
  compile 'org.wordpress:persistentedittext:1.0.1'
  compile 'com.mcxiaoke.volley:library:1.0.16'
  androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.0'
  androidTestCompile 'org.objenesis:objenesis:2.1'
  androidTestCompile 'org.mockito:mockito-core:+'
  androidTestCompile 'org.robolectric:robolectric:3.0'
  // Provided by the WordPress-Android Repository
  compile 'org.wordpress:gcm:1.0.0'
  // not found in maven central
  compile 'org.wordpress:drag-sort-listview:0.6.1'
  // not found in maven central
  compile 'org.wordpress:slidinguppanel:1.0.0'
  // not found in maven central

  //compile 'org.wordpress:android-passcodelock:0.0.6'
  compile 'org.wordpress:emailchecker:0.3'
  // Simperium
  compile 'com.simperium.android:simperium:0.6.4'
  releaseCompile project(path: ':libs:utils:WordPressUtils', configuration: 'release')
  debugCompile project(path: ':libs:utils:WordPressUtils', configuration: 'debug')
  releaseCompile project(path: ':libs:networking:WordPressNetworking', configuration: 'release')
  debugCompile project(path: ':libs:networking:WordPressNetworking', configuration: 'debug')
  releaseCompile project(path: ':libs:analytics:WordPressAnalytics', configuration: 'release')
  debugCompile project(path: ':libs:analytics:WordPressAnalytics', configuration: 'debug')
  releaseCompile project(path: ':libs:editor:WordPressEditor', configuration: 'release')
  debugCompile project(path: ':libs:editor:WordPressEditor', configuration: 'debug')
  compile project(path: ':libs:PasscodeLock:library')
  compile files('../libs/svg-android-1.1.jar')
  compile 'info.hoang8f:fbutton:1.0.5'
  compile 'com.android.support:support-v4:+'
  compile 'com.squareup.picasso:picasso:2.3.2'
  compile 'com.nineoldandroids:library:2.4.0'
  compile 'com.daimajia.slider:library:1.1.5@aar'
  compile 'com.github.traex.expandablelayout:library:1.3'
  compile 'com.github.gabrielemariotti.cards:cardslib-cards:2.0.1'
  compile files('../libs/svg-android-1.1.jar')
  compile files('../libs/android-viewbadger.jar')
  compile 'com.diogobernardino:williamchart:2.0.1'
  //    compile 'com.github.traex.rippleeffect:library:1.3'

  //    fix for ripple crash android 6.0
  compile 'com.github.Hitta:RippleEffect:82cf00e551'
  compile 'com.github.citux:datetimepicker:0.2.0'
  //    compile 'com.google.android.gms:play-services-appindexing:8.1.0'
  compile('com.crashlytics.sdk.android:crashlytics-ndk:1.1.5@aar') {
    transitive = true;
  }
  compile 'com.google.android.gms:play-services:10.0.1'
  compile 'com.google.android.gms:play-services-maps:10.0.1'
  compile 'com.google.firebase:firebase-core:10.0.1'
  compile 'com.google.firebase:firebase-messaging:10.0.1'
}

configurations.all {
  // Exclude packaged wordpress sub projects, force the use of the source project
  // (eg. use :libs:utils:WordPressUtils instead of 'org.wordpress:utils')
  exclude group: 'org.wordpress', module: 'utils'
  exclude group: 'org.wordpress', module: 'analytics'
  exclude module: 'jsr305'
  exclude module: 'jcip-annotations'
}

task generateCrashlyticsConfig(group: "generate", description: "Generate Crashlytics config") {
  def outputFile = new File("${rootDir}/WordPress/crashlytics.properties")
  def inputFile = file("${rootDir}/WordPress/gradle.properties")
  if (!inputFile.exists()) {
    throw new StopActionException(
        "Build configuration file:" + inputFile + " doesn't exist, follow README instructions")
  }
  outputs.file outputFile
  inputs.file inputFile
  doLast {
    def properties = new Properties()
    inputFile.withInputStream { stream -> properties.load(stream)
    }
    def crashlyticsApiKey = properties.getProperty('crashlytics.apikey', '0')
    def writer = new FileWriter(outputFile)
    writer.write("""// auto-generated file from ${rootDir}/gradle.properties do not modify
apiKey=${crashlyticsApiKey}""")
    writer.close()
  }
}

// Add generateCrashlyticsConfig to all generateBuildConfig tasks (all variants)
android.applicationVariants.all {
  variant -> variant.generateBuildConfig.dependsOn(generateCrashlyticsConfig)
}

// Add properties named "wp.xxx" to our BuildConfig
android.buildTypes.all { buildType ->
  project.properties.any { property ->
    if (property.key.toLowerCase().startsWith("wp.")) {
      buildType.buildConfigField "String",
          property.key.replace("wp.", "").replace(".", "_").toUpperCase(),
          "\"${property.value}\""
    }
  }
}

// For app signing
if (["storeFile", "storePassword", "keyAlias", "keyPassword"].count {
  !project.hasProperty(it)
} == 0) {
  android {

    signingConfigs {
      release {
        storeFile = file(project.storeFile)
        storePassword = project.storePassword
        keyAlias = project.keyAlias
        keyPassword = project.keyPassword
      }
    }
  }
  android.buildTypes.release.signingConfig = android.signingConfigs.release
}
crashlytics {
  enableNdk true
  androidNdkOut 'src/main/obj'
  androidNdkLibsOut 'src/main/libs'
}

apply plugin: 'com.google.gms.google-services'

